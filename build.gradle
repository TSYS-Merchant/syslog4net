buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.ullink.gradle:gradle-msbuild-plugin:2.11'
    }
}

version = '1.0.15-SNAPSHOT'
apply plugin: 'msbuild'

// You can't have non-digits in a DLL version number
project.ext.set("baseVersion", project.version.minus('-SNAPSHOT'))

def user = System.getProperty("user.name")
if ((user == 'jenkins') || (user == 'buildservice')) {
    project.ext.set("buildNumber", System.getenv("BUILD_NUMBER"))
    project.ext.set("assemblyVersion", project.baseVersion + '.' + project.buildNumber)
} else {
    project.ext.set("buildNumber", user + '-' + new Date().format("yyyyMMdd'T'HHmmss'Z'", TimeZone.getTimeZone('UTC')))
    project.ext.set("assemblyVersion", project.baseVersion + '.0')
}


msbuild {
    projectFile = 'src/main/dot-net/Build.proj'
    projectName = project.name
    targets = ['Clean', 'Build']
    configuration = 'Release'
    parameters.version = project.version
    parameters.assemblyVersion = project.assemblyVersion
    parameters.buildNumber = project.buildNumber
}
msbuild.outputs.upToDateWhen { false }

build
build.dependsOn msbuild

task packageNuget(type: Exec) {
    workingDir 'src/main/dot-net/syslog4net'
    commandLine 'cmd', '/c', 'NuGet.exe', 'pack', 'syslog4net.csproj', '-Prop', 'Configuration=Release', '-Version', project.baseVersion
}
packageNuget.dependsOn build
packageNuget.outputs.upToDateWhen { false }
